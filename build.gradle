buildscript {
  repositories {
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath 'com.develhack:gradle-eclipse-plugin:+'
    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:+'
  }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.develhack.eclipse'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.jfrog.bintray'

group = 'com.develhack'
archivesBaseName = 'develhack-core'
version = '0.9.1'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories { mavenCentral() }

dependencies {
    testCompile 'org.testng:testng:6.8.+'
}

test {
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

javadoc {

    options {
        locale = 'en_US'
        links 'http://docs.oracle.com/javase/8/docs/api/'
    }

    title = "Develhack Core Library API (${version})"
    destinationDir = file("${docsDir}/javadoc/${version}")
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task javadocJar(type: Jar) {
    dependsOn 'javadoc'
    from "${buildDir}/docs/javadoc"
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar { classifier "sources" }
            artifact javadocJar { classifier "javadoc" }
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name project.name
                    description 'The underlying library for Develhack.'
                    url 'https://github.com/develhack/develhack-core'
                    scm {
                        url 'https://github.com/develhack/develhack-core.git'
                        connection 'https://github.com/develhack/develhack-core.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'develhack'
                            name 'Develhack.com'
                        }
                    }
                }
            }
        }
    }
}

if(hasProperty('BINTRAY_USER') && hasProperty('BINTRAY_API_KEY')) {

    bintray {
        user = "$BINTRAY_USER"
        key = "$BINTRAY_API_KEY"
        publications = ['mavenJava']
        pkg {
            userOrg = 'develhack'
            repo = 'maven'
            name = 'develhack-core'
        }
    }
}

defaultTasks 'clean', 'build'
